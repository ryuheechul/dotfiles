# vim: set ft=zsh:

function _tag {
  return # comment this out in order to debug performance
  echo shell_ext $1
  date +%s%N
}

_tag start

# function add-to-path # this function should be defined at ../path/set-basic

function evalcache-clear {
  yes | _evalcache_clear
}

# to make it consumable for _evalcache
function _evalable {
  # echo is the key to make it consumable
  # subshell substituting is the key to cache the result
  print "echo -n '$(${@})'"

  # meaningless example of this function would be be low which was my mistake in the past
  # echo "$@"
}

# alternative to call _evalable arch directly to produce better filename for cached results
function _evalable_arch {
  _evalable arch
}

function _evalable_fzf_share {
  _evalable fzf-share
}

function _evalable_brew_prefix {
  _evalable brew --prefix
}

function _evalable_yarn_global_path {
  _evalable yarn global bin
}

function _evalable_vivid_generate_solarized_light {
  _evalable vivid generate solarized-light
}

_tag fn-def

if is-nixos; then
  # this is mainly for nixd at ../../nvim/lua/plugins/config/lsp-servers.lua but also why not
  export NIX_PATH="$(${my_dot_d}/nix/niv-shim/bin/nix-path-via-niv.sh nixpkgs=nixos:nixos-hardware:home-manager):nixos-config=${my_dot_d}/bootstrap/foundation/nixos/configuration.nix"
  _tag nix
fi

_arch="$(_evalcache _evalable_arch)"

# intel chip (at least not powerful ones) can't seem to handle this, so disabled for them for now
test "${_arch}" = "arm64" && {
  export my_zsh_plugin_zsh_autocomplete=1;
  export my_nvim_tabnine=1;
}

# currently it bring very poor performance issue so turning it off for now
unset my_zsh_plugin_zsh_autocomplete;

_tag arch

# avoid having to type `docker buildx build` and `docker build` would do the same with this var set
export DOCKER_BUILDKIT=1

# difftastic options see `difft --help` for more
# export DFT_DISPLAY=inline # but comment out for now as side-by-side actually complements with the usual diff as difftool

# `-j.5`: https://superuser.com/questions/257605/how-to-center-search-results-in-less-instead-of-moving-matched-line-to-top-of-t#comment266859_257626
export LESS='-j.5 --RAW-CONTROL-CHARS --IGNORE-CASE'
export SYSTEMD_LESS="${LESS} -FRSXMK" # since systemd tools (i.e. journalctl, systemctl, etc.) ignore `$LESS`

# theme for bat
export BAT_THEME="Solarized (light)"
# set it as env var here instead of ~/.config/bat/config as the issue is described in the file
export BAT_PAGER="less --quit-if-one-screen --mouse"

# set LS_COLORS with vivid
if fastcommandcheck vivid; then
  export LS_COLORS="$(_evalcache _evalable_vivid_generate_solarized_light)"
fi

_tag vivid

# fzf + fd # fd ignores .gitignore files/directories by default
# default
export FZF_DEFAULT_COMMAND='fd --type f'
# color is based on https://github.com/junegunn/fzf/wiki/Color-schemes#solarized-light
export FZF_DEFAULT_OPTS='
  --height 40%
  --layout=reverse
  --color fg:240,hl:33,fg+:241,bg+:221,hl+:33
  --color info:33,prompt:33,pointer:166,marker:166,spinner:33
  '

# CTRL-T - Paste the selected files and directories onto the command-line
export FZF_CTRL_T_COMMAND='fd'
export FZF_CTRL_T_OPTS='--preview "preview {}"'

# ALT-C - cd into the selected directory
export FZF_ALT_C_COMMAND='fd -t d'
export FZF_ALT_C_OPTS='--preview "preview {}"'

# better history search with fzf via https://nixos.wiki/wiki/Fzf

if fastcommandcheck fzf-share; then
  path_from_fzf_share="$(_evalcache _evalable_fzf_share)"
  source "${path_from_fzf_share}/key-bindings.zsh"
  source "${path_from_fzf_share}/completion.zsh"
else # falling back to brew
  # in case of enabling with brew via https://github.com/junegunn/fzf#using-homebrew
  if fastcommandcheck brew; then
    [ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
    # configuring completions for brew installed packages
    # this FPATH will be needed in ../zshrc and should be accessible this way too
    FPATH="$(_evalcache _evalable_brew_prefix)/share/zsh/site-functions:$FPATH"
  fi
fi

_tag fzf

## binaries by language global packages, for example, npm, rust, go, etc.

# rust
if fastcommandcheck cargo; then
  add-to-path "${HOME}/.cargo/bin"
fi

_tag cargo

# golang
if fastcommandcheck go; then
  export GOPATH=${GOPATH:-${HOME}/go}
  add-to-path "${GOPATH}/bin"
fi

_tag go

# yarn
if fastcommandcheck yarn; then
  # because yarn global bin is slow
  add-to-path "$(_evalcache _evalable_yarn_global_path)"
fi

_tag yarn

# asdf
ASDF_DIR="${ASDF_DIR:-${HOME}/.asdf}"
# https://github.com/asdf-vm/asdf - version manager
[ -f "${ASDF_DIR}/asdf.sh" ] && source "${ASDF_DIR}/asdf.sh"

_tag asdf

# to make sure my custom paths supersedes default ones like /bin /usr/bin /usr/local/bin again
float_nix_path=1 source "${my_zsh_d}/path/set-basic"

_tag my_paths_supersedes


# use direnv
fastcommandcheck direnv && _evalcache direnv hook zsh

_tag direnv

# for https://github.com/aykamko/tag
if fastcommandcheck tag; then
  export TAG_SEARCH_PROG=ag
  tag() { command tag "$@"; source ${TAG_ALIAS_FILE:-/tmp/tag_aliases} 2>/dev/null }
  alias ag=tag
fi

_tag tag

# CTRL-G - https://github.com/denisidoro/navi#usage
fastcommandcheck navi && _evalcache navi widget zsh

_tag navi

# fallback any command to npx # disabled for now
# source <(npx --shell-auto-fallback zsh)

## base16-shell supports

function should_skip_loading_base16_shell()
{
  if is-ssh || test -n "${INSIDE_EMACS}" || test -n "${VIMRUNTIME}" || test -n "${NVIM}" ; then
    return 0 # true
  else
    return 1 # false
  fi
}

if should_skip_loading_base16_shell; then
  true
else
  BASE16_SHELL="${BASE16_SHELL:-${HOME}/.config/base16-shell}"
  [ -n "${PS1}" ] && \
      [ -s "${BASE16_SHELL}/profile_helper.sh" ] && \
          source "${BASE16_SHELL}/profile_helper.sh"

  # to initialize a theme when there is none
  test -f ~/.base16_theme || light

  # shim to workaround the issue with cached result of above
  export BASE16_THEME="$(current-base16)"

  source ~/.base16_theme

  _tag base16
fi


# detect podman only environment and make it easy for other apps like `lazydocker` or `../../nix/pkgs/custom/termimagenator.nix` (which relies on docker) to work
if test -z "${DOCKER_HOST}" && test -S "${XDG_RUNTIME_DIR}/podman/podman.sock" && which docker | xargs readlink -f | egrep 'podman$' >/dev/null; then
  # running `systemctl --user start podman.socket` (once) will be required for "${XDG_RUNTIME_DIR}/podman/podman.sock" to exist
  export DOCKER_HOST="unix://${XDG_RUNTIME_DIR}/podman/podman.sock"
  if test -S /run/docker.sock; then
    echo "[INFO] \$DOCKER_HOST is set to '${DOCKER_HOST}'"
    echo "[WARN] \`/run/docker.sock\` still exists" 
    if readlink -f /run/docker.sock | egrep 'podman.sock$' >/dev/null; then
      echo "[WARN] and it points to $(readlink -f /run/docker.sock)"
    fi
    echo "[INFO] you might want to delete \`/run/docker.sock\` file if it's uncollected garbage from an inactive (rootful) docker daemon"
    echo "[INFO] you can find this error message by running \`show-docker-host-err-message\`"
    alias show-docker-host-err-message="cat \"${my_zsh_d}/my_addons/shell_ext\""
    sleep 2
  fi

  _tag podman_docker_shim
fi

if test -n "${TMUX_PANE}"; then
  tmux-which-key() { tmux show-wk-menu-root; }
  zle -N tmux-which-key
  bindkey -M vicmd " " tmux-which-key
fi
if test -n "${ZELLIJ_SESSION_NAME}"; then
  zellij-which-key() { zellij action switch-mode normal; }
  zle -N zellij-which-key
  bindkey -M vicmd " " zellij-which-key
fi
_tag mux_which_key

# although home-manager might enable this,
# it's also ignored by ../../nix/home/programs/shells.nix
# hence relying on bash's one instead
command_not_found_handler() {
  # assuming bash has its own handler
  bash -ic "command_not_found_handle ${1}"
}

unfunction _tag
