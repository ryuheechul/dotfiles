# vim: set ft=zsh:

# define functions first so that it doesn't get affected by aliases being defined here

# to measure zsh performance - https://blog.mattclemente.com/2020/06/26/oh-my-zsh-slow-to-load.html
function timezsh() {
  shell=${1-$SHELL}
  for i in $(seq 1 10); do /usr/bin/time $shell -i -c exit; done
}

## these are convenient functions for getting information from package.json
## perhaps should be moved somewhere else than this file
function grep-npm-packages-with-keyword () {
  { echo '{' ; grep $1 package.json ; echo '}' } \
    | jq -n -f /dev/stdin \
    | jq -r 'keys | .[]' \
    | xargs
}

function grep-deleted-npm-packages () {
  function _grep-deleted-npm-packages () {
    gd --unified=0 package.json | cat | egrep -- '^- ' | sed s/-//
  }

  { echo '{' ; _grep-deleted-npm-packages ; echo '}' } \
    | jq -n -f /dev/stdin \
    | jq -r 'keys | .[]' \
    | xargs
}

## these benches can be used to compare between different systems
function bench () {
  start=$(date +%s)
  ${1}
  end=$(date +%s)
  echo "bench took $(($end-$start))s"
}

# from https://www.pcsuggest.com/benchmarking-linux-systems-with-simple-command-line-tools/
function simple_test_cpu () {
  i=0; while (( i < 9999999 )); do (( i ++ )); done
}

function simple_bench_cpu () {
  bench simple_test_cpu
}

# from https://cloudzy.com/knowledge-base/test-disk-speed-in-linux/
function simple_test_disk () {
  sync; dd if=/dev/zero of=temp_disk_test_file bs=1M count=1024; sync
  rm temp_disk_test_file
}

function simple_bench_disk () {
  bench simple_test_disk
}

# when I don't feel liking adding `| less` at the end
function show () {
  "$@" | less
}

# use help like man
function help () {
  show "$@" --help
}

# quickly jump via fsad - https://github.com/junegunn/fzf/wiki/examples#with-fasd-1
unalias j 2>/dev/null
function j() {
    [ $# -gt 0 ] && fasd_cd -d "$*" && return
    local dir
    dir="$(fasd -Rdl "$1" | fzf -1 -0 --no-sort +m)" && cd "${dir}" || return 1
}

if is-darwin; then
  function _sound_for_return() {
    expr $? = 0 > /dev/null && afplay /System/Library/Sounds/Funk.aiff || afplay /System/Library/Sounds/Purr.aiff
  }

  # getting notified on a job that takes some time (usually with `make`) turns out to be very helpful
  function notify() {
    start_time=$(date +%s)

    env time -p "$@"
    ret=$?

    # skip making sound for under 2 seconds
    test $(expr $(date +%s) - ${start_time}) -gt 2 && {
      expr ${ret} = 0 > /dev/null ; _sound_for_return
    }

    # print the date just before exit
    date

    # return the original result
    return ${ret}
  }

  # able to hook up completions for arbitrary command arguments
  # thanks to great hints from https://www.dolthub.com/blog/2021-11-15-zsh-completions-with-subcommands/
  # but this does not work well with subcommands of the commands at the moment.
  # but since `make` has no more depth it's OK for now.
  _notify() {
    local line state

    _arguments -C "1: :->cmds" "*::arg:->args"

    case "$state" in
      cmds)
        _exec # borrowing `_exec` completion function to list all commands
        ;;
      args)
        _${line[1]} # assume the command has its own completion function like `_make` defined
        ;;
    esac
  }

  # since compdef on regular sourcing doesn't seem to work
  zsh-defer compdef _notify notify

  alias make='notify make'

  # shorthand for other commands other than make - eg. `nt true` and `nt false`
  alias nt='notify'
fi

# this will return either `light` or `dark`
function theme-tone () {
  # assuming I only use `solarized-*`
  current-base16 | sed s/solarized-//
  # although below "works" it doesn't work well with my cached base16 shell eval - look at the end of ./shell_ext
  # printenv BASE16_THEME | sed s/solarized-//
}

# a helper also that warns about the `git reset --hard`, read more at https://jwiegley.github.io/git-from-the-bottom-up/3-Reset/4-doing-a-hard-reset.html
# I indeed accidentally blow some unstaged changes few times... hence this function to prevent that
function git-reset-hard() {
  echo '`git reset --hard` will permanently delete unstaged changes, so you should run `git stash` before and run `git stash pop` after. And I will print `git status` for you.'

  git status

  gum confirm "continue? (CHECK IF YOU NEED TO STASH BEFORE)" && {
    selected=$(git log --oneline | gum filter --placeholder "choose the commit...")
    hash="$(echo "${selected}" | awk '{ print $1 }')"
    commit_log="$(echo "${selected}" | awk '{ $1 = ""; print $0 }' |sed 's/^ //g')"

    echo "you chose \"${selected}\""

    cmd_to_run="git reset --hard ${hash}"
    gum confirm "run \`${cmd_to_run}\`? (MAKE SURE YOU STASHED UNSTAGED ONES)" && {
      which git
      echo "running \`${cmd_to_run}\`..."
      bash -c "${cmd_to_run}"
      echo "running \`git stash list\`"
      git stash list
      echo 'now you probably want to run `git stash pop` if you stashed before'
    }
  }
}

# a helper script to quickly set git author in global config
function git-config-author() {
  function _print_config() {
    echo "git config --global user.${1}"
    git config --global user.${1}
  }

  echo 'printing current values...'
  _print_config name
  _print_config email
  echo

  echo 'git config --global user.name:'
  gum input --placeholder 'full name' | xargs -I _ git config --global user.name _
  _print_config name

  echo 'git config --global user.email:'
  gum input --placeholder 'email@address' | xargs -I _ git config --global user.email _
  _print_config email
}

# workaround to use SSH URL instead of HTTP
function git-override-url-to-ssh {
  if git rev-parse --show-toplevel > /dev/null; then
    config_path="$(git rev-parse --show-toplevel)/.git/config"

    cat <<EOF >> "${config_path}"

# No longer having to rewrite http urls to ssh ones manually
# thanks to https://www.jvt.me/posts/2019/03/20/git-rewrite-url-https-ssh
[url "ssh://git@github.com/"]
  insteadOf = https://github.com/
[url "ssh://git@gitlab.com/"]
  insteadOf = https://gitlab.com/
EOF

    echo "cat ${config_path}"
    cat "${config_path}"
fi
}

# to make this to surive through piping; let's see if this is a good idea
alias grc='grc --colour=on'

# https://www.stefanjudis.com/today-i-learned/suffix-aliases-in-zsh/
alias -s md=glow
alias -s {ts,tsx,js,jsx,json,yaml,yml,toml}=vi
alias -s {py,lua,nix}=vi
alias -s {conf,}=lf
alias -s {org,el}='emacsclient'
alias -s git='git clone'

## better defaults
alias rsync='rsync --exclude=.git' # prevents disasters of overriding git history in case local is not fully synced with remote yet
alias shellcheck='shellcheck --color=always'
alias distrobox='STARTSHIP_SESSION_KEY= UNSET_ALL_MY_ZSH_STUFF_LOADED=1 distrobox'
alias gh='GLAMOUR_STYLE=$(theme-tone) gh'
# equip auxww by default for convenience: basically show all process with full command
alias ps='grc ps auxww'
# better than bare info but still not the best
alias info='info --vi-keys'
# ignore case on searching with keyword
alias man='man -i'; fastcommandcheck batman && { zsh-defer compdef _man batman; alias man='batman'; }
# replace htop with bottom
alias htop='btm -b'
# overwrite essential stuff
alias which='which -a'
alias stat='grc stat'
alias ping='grc ping'
alias ping='gping'
alias traceroute='grc traceroute'
alias ip='ip -c'
# colorize `dig` or there is also `dog`
alias dig='grc dig +search' # host and dog seems to search by default but not dig - https://serverfault.com/a/434583/633069

# human friendly df and du
alias df='duf'
alias du='grc du -d 1 -h | sort -h -r' # or there is `dust` as an alternative

# help avoiding seeing diff for long generated files - somehow `:(exclude)**/[filename]` doesn't seem to cover some cases so `:(exclude)[filename]` to complement it
exclude_for_pure="-- ':(exclude,top)package-lock.json' ':(exclude,top)**/package-lock.json' ':(exclude,top)pnpm-lock.yaml' ':(exclude,top)**/pnpm-lock.yaml' ':(exclude,top)lazy-lock.json' ':(exclude,top)**/lazy-lock.json'"
# git
alias tig='TERM=xterm-256color tig'
alias gs='pwd; ls; git status'
alias tgs='tig status'
alias gd='git diff'
alias gdc='gd --cached'
alias gdt='git difftool'
alias gdtc='gdt --cached'
alias gdp="gd ${exclude_for_pure}"
alias gdcp="gdc ${exclude_for_pure}"
alias gdtp="gdt ${exclude_for_pure}"
alias gdtcp="gdtc ${exclude_for_pure}"
# and just to help with typo
alias gd.='gd .'
alias gdc.='gdc .'

# do this again although this is done by ../aliases already since this get overwritten by something along the way (probably OMZ)
alias l="ls -lah -g --git"

 # https://www.reddit.com/r/bash/comments/tkftlv/comment/iuhhz9g
_nclr () { local j; for ((j = 0; j <= "${1:-1}"; j++ )); do tput cuu1; done; tput ed; }

# quick way to navigate tmux windows - see also ../../nvim/lua/plugins/keymaps.lua

if test -n "${TMUX_PANE}"; then
  alias wt='tmux choose-tree; _nclr 2'
  alias wn='tmux new-window; _nclr 2'
  alias wp='tmux split-window -h; _nclr 2'
  alias w/='tmux split-window -h; _nclr 2'
  alias w-='tmux split-window -v; _nclr 2'
fi
if test -n "${ZELLIJ_SESSION_NAME}"; then
  alias wt='zellij action launch-or-focus-plugin -f sessionpicker; _nclr 2'
  alias wn='zellij action new-tab; _nclr 2'
  alias wp='zellij action new-pane; _nclr 2'
  alias w/='zellij action new-pane -d "right"; _nclr 2'
  alias w-='zellij action new-pane -d "down"; _nclr 2'
fi

# this overwrites existing gap which is for git apply
alias gap='git add -p'

# git fix remote to ssh from https
alias gfssh="ambr --no-parent-ignore 'https://' 'ssh://git@' .git/config"

# editor
alias _pathnvimlazy='echo "${HOME}/.local/share/nvim/lazy"'
alias pathnvimlazy='_pathnvimlazy | pbcopy ; _pathnvimlazy'
alias clearnvimswaps='rm -r ~/.local/state/nvim/swap'
alias vim='nvim'
alias vi='vim'

_dotfilepath="$(readlink -f ~/.config/dfs-rhc)"
_perflogpath="${_dotfilepath}/.perf.log"
## special modes
# start neovim purely without any plugins loaded
alias vil='my_nvim_plugins_default_to_lazy=1 vi'
alias vis='my_nvim_use_sg=1 vi'
alias vip='vi -u NONE'
# measure startup time
alias vips="rm -f ${_perflogpath} && vi --startuptime ${_perflogpath} ${_perflogpath}"
# to enable LuaCacheProfile
alias cim='NVIM_CACHE_PROFILE=1 vi'
# start neovim to profile interactively
alias pim='NVIM_PROFILE=1 vi'
# neovim with ghost
alias vig='my_nvim_ghost=1 vi'

# usage: sggh ryuheechul/hexto256 main.go
sggh ()
{
  vis "sg://github.com/${1}/-/${2}"
}

# wrap `sk` to use TMUX_PANE when possible
sk ()
{
  # prevent running sk on home directory as there are too many things to read
  test "${PWD}" = "${HOME}" && { echo "${PWD}"; return; }

  if test -n "${TMUX_PANE}"; then
    sk-tmux "$@"
  else
    env sk "$@"
  fi
}

skp ()
{
  # because sk seems to reset env vars unlike with the case of fzfp
  sk --preview "BAT_THEME='${BAT_THEME}' preview {}"
}

## convenient quick ways to select file to open in vim
# 1. in the current directory
alias v='skp | xargs ${EDITOR}'
# 2. in the git root directory
alias vv='grt; v; popd' # basically - `git rev-parse --show-toplevel || echo . | xargs pushd; v; popd`
# 3. my dotfiles
alias dot='pushd "${my_dot_d}"; v; popd'

# similar to `v` but with code searching via `cs`
alias c='cs | xargs ${EDITOR}'

# useful when the status of lazy.nvim seems not clean
# for now, it only print the command not run
alias clean-nvim-cmd='echo "rm -rf  ~/.local/share/nvim/lazy ~/.cache/nvim/lazy"'

# open in terminal using daemon process (create if it doesn't exist yet)
alias emacsclient="emacsclient -nw -c -a ''"

# just a dirty hack to make `emacs` command to use a handle which work more smoothly
# in terms of being able to activate the window like any other app on macOS
typeset -f emacs &> /dev/null && unfunction emacs
function emacs () {
  if test -n "${SSH_CONNECTION}"; then
    env emacs -nw ${@} # use `emacs` instead of `emacsclient` on SSH connection
  else
    if test $# -eq 0; then
      if is-darwin; then
        "${my_dot_d}/bin/path/darwin/open-gui-emacs"
      else
        emacsclient
      fi
    else
      emacsclient ${@}
    fi
  fi
}

# viewer and filter
alias vimdiff='nvim -dR'
alias diff='git diff --no-index'
alias cat='bat'
alias k9s='TERM=xterm COLORTERM= k9s'
alias delta='delta --detect-dark-light=never'

## ripgrep
# alias rg='rg -p -i' # with pretty (-p) and ignore case (-i)
# enhanced the above alias using the function below to apply what's mentioned at https://github.com/BurntSushi/ripgrep/issues/86#issuecomment-1469717706
rg ()
{
  env rg -i --json "$@" | delta
}
alias rgc='rg -C' # with context
alias rgc5='rgc 5' # with context value that is 5

alias glow='glow -p -s "$(theme-tone)"'

alias fzfp='fzf --height 100% --preview "preview {}"'
alias fzfpp='fzfp|fpp'
alias fpp='wrapped-fpp'

# diff with bat
function bdiff () { diff -u "$@" | bat;} # use `vimdiff` for editing
alias build-cache-bat='bat cache --build' # when there is a new syntax/theme to build

# shorthands
alias dc='docker-compose'
alias mp='multipass'
alias tf='terraform'
alias cht='CHTSH_QUERY_OPTIONS="style=friendly" cht.sh'
alias chtsh='cht --shell'

# web search
alias googler='googler -n 3'
alias ddgr='ddgr -n 3'

# https://nixos.wiki/wiki/Storage_optimization
alias nix-collect-garbage='nix-collect-garbage --delete-older-than 30d'
is-nixos && alias nix-collect-garbage='nix-collect-garbage --delete-older-than 30d; sudo nix-collect-garbage --delete-older-than 30d'
# set half of the available core to be default
# to prevent OOM during paralleled compilations
# to be kind for low memory environments
# https://nixos.org/manual/nix/stable/advanced-topics/cores-vs-jobs.html
export NIX_BUILD_CORES="$(getconf _NPROCESSORS_ONLN | xargs -I _ expr _ / 2)"

alias switch-home='home-manager switch |& nom && evalcache-clear >/dev/null && bat cache --build'

# unifying interface of applying updated nix expressions for different systems
alias switch-nix='switch-home' # this is default for a simple system that only uses home-manager
# on darwin, it will also do "switch" via nix-darwin
is-darwin \
  && alias switch-nix='switch-home; pushd "$my_dot_d/nix/darwin" && make switch; popd'
# on nixos, rely on one script to do it all
is-nixos \
  && alias switch-nix-dry='"$my_dot_d/bootstrap/foundation/nixos/nixos-rebuild.sh"' \
  && alias switch-nix='"$my_dot_d/bootstrap/foundation/nixos/switch.sh"'

# run this with "current configuration" before having to build anything new so that `/boot` can be cleaned first to make space
# thanks to https://discourse.nixos.org/t/what-to-do-with-a-full-boot-partition/2049/4
is-nixos \
  && alias clean-boot='sudo nix-env -p /nix/var/nix/profiles/system --delete-generations +3 && sudo nix-collect-garbage && ls /nix/var/nix/profiles/system && df'

# quicker way to reboot by skipping hardware checks but subsequent quick reboot may result in coma
is-linux && alias quickreboot='sudo systemctl kexec'

# commandify
alias neofetch='fastfetch'
alias fetch='neofetch; ipfetch'
alias reenter='cd ~ && cd -'
alias fulltimezsh='FORCE_LOAD_MY_ZSH_STUFF=1 timezsh'
alias vzshrc='nvim ~/.local.zshrc ~/.zlogin ~/.zshenv ~/.zshrc'
alias sshconfig='cat ~/.ssh/config'
alias vsshconfig='nvim ~/.ssh/config'
alias touchreadme='touch README.md'
alias touchmakefile='touch Makefile'
alias _shebang='echo "#!/usr/bin/env bash"'
alias shebang='_shebang | pbcopy ; _shebang'
alias executable='chmod +x'
alias lf='UNSET_ALL_MY_ZSH_STUFF_LOADED=1 lf'
alias quickvenv='python3 -m venv "${MY_QUICK_VENV_PATH:-.venv}" && source "${MY_QUICK_VENV_PATH:-.venv}/bin/activate"'
alias quickserve='nix-shell -p caddy --run "caddy file-server --browse -l :9090"'
alias su='sudo su' # `sudo -i` or `sudo --login` can do the similar thing in a simple case - https://www.redhat.com/en/blog/difference-between-sudo-su

alias nis='npm install --save'
alias nisd='npm install --save-dev'
alias whoami-aws='aws sts get-caller-identity'
alias verify-zellij='zellij setup --check'

# firstly fallback to `fg` when there is a background job is running
# secondly exit virtualenv if the shell is inside one
# lastly `exit` the shell when there no other conditions above are met
alias q='jobs | wc -l | xargs test 0 -eq && { test -n "${VIRTUAL_ENV}" && type deactivate && deactivate || exit } || fg'
alias e='fzf | xargs ${EDITOR}'
alias jqstringify='jq ". | tostring"'
alias jqfromjson='jq "fromjson"'
# more on it at https://www.cyberciti.biz/faq/unix-linux-check-if-port-is-in-use-command/
alias listopenports='lsof -i -P -n | grep LISTEN'

## quick ways to grep from espanso
# eml: only trigger the match
alias eml="espanso match list | fzf | sed -r 's/^(.*)[ ]-[ ].*$/\1/' | xargs espanso match exec -t"
# emls: trigger + sink the result into clipboard - use `vared` (similar to `read` in zsh) to sink the "typing" from espanso since it becomes stdin
alias emls="eml && vared -p '[Press â†µ to continue] ' -c _sink_espanso && echo \"\$_sink_espanso\" | tee /dev/tty | pbcopy"
# emlg: do the same as above but using builtin GUI
alias emlg="espanso cmd search" # or ALT + SPACE

_expand_newlines_unquoted_only()
{
  # line 1 - sed: mark `\\n` wrapped in '' to `tO_Restore-NEW_linE`
  # line 2 - sed: mark `\\n` wrapped in "" to `tO_Restore-NEW_linE`
  # line 3 - sed: now remained replace `\\n` to `\n` (actual newline)
  # line 4 - sed: finally replace `tO_Restore-NEW_linE` to `\\n` (not actual newline)
  sed -r ':repeat;s/["](.*)(\\n)(.*)["]/"\1tO_Restore-NEW_linE\3"/g;t repeat' \
    | sed -r ":repeat;s/['](.*)(\\\n)(.*)[']/'\1tO_Restore-NEW_linE\3'/g;t repeat" \
    | sed "s/\\\n/\n/g" \
    | sed "s/tO_Restore-NEW_linE/\\\n/g"

  # about sed repeat - https://stackoverflow.com/a/44786664/1570165
}

# a helper to grep from history to pbcopy                                 | uniq            | strip          |
# break it down  = history | recent first   | strip line  numbers         | for unsorted*   | leading spaces | strip one word commands    | fzf
alias grephistory='history | sort --reverse | sed "s/^\s*[0-9]*[*]*\s*//" | awk "!x[\$0]++" | sed "s/^\s*//" | sed "/^[a-zA-Z0-9]*\s*$/d" \
  | fzf | tr -d "\\n"      | _expand_newlines_unquoted_only | tee /dev/tty | pbcopy    ; echo'
# | fzf | delete the       | expand newlines                | print        | copy to   ; add newline
#       | trailing newline |                                | selected     | clipboard ;
# *about uniq for unsorted - https://github.com/junegunn/fzf/issues/270#issuecomment-1301876481

# debug helpers
latest-path-home-manager ()
{
  local gen="$(home-manager generations | head -n1 | awk '{ print $5; }')"
  echo "/nix/var/nix/profiles/per-user/${USER}/home-manager-${gen}-link"
}

inspect-home-manager ()
{
  latest-path-home-manager | xargs -I _ zsh -c 'tree '_'/'
  latest-path-home-manager | xargs lf
}

# follow the link of the command
# useful to trace symlinked binaries the ones from nixpkgs
which-trace ()
{
  which $1 | fzf | xargs realpath
}

# useful to browse other binaries in the same directory
which-browse ()
{
  which-trace $1 | xargs lf
}

# `w` to see deps (relative to the current root) `/` to search
alias nix-tree-profile='nix-tree ~/.nix-profile'
alias nix-tree-system='nix-tree /run/current-system'
# https://github.com/utdemir/nix-tree/issues/26
alias nix-deps='fzf | xargs -o nix-tree' # e.g. which lxc | nix-deps
alias nix-referrers='fzf | xargs nix-store --query --referrers' # e.g. which lxc | nix-referrers

alias inspect-bash='/bin/bash -lixc exit 2>&1 | sed -n "s/^+* \(source\|\.\) //p"' # via https://stackoverflow.com/a/65922714/1570165
alias inspect-iommu="bash -c 'dmesg | rg IOMMU'" # wrap with bash in mind of being called with other command such as sudo
alias dmesg='sudo dmesg -H --nopager --color=always | less' # since dmesg doesn't respect PAGER nor seem to use less

# mimic `command-not-found` via `nix-index` shell hook when it's not available
fastcommandcheck command-not-found || alias command-not-found='bash -ic '
