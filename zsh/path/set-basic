# -*- mode: sh; -*- vim: set ft=zsh:

# source this file (even multiple times when needed to make sure the order of path is ensured)

# avoid $PATH polluted by duplicates - also new ones take precedence
typeset -U path

# convinient way to print $PATH
alias printpath='echo ${path} | tr " " "\n"'

function _load_brew() {
  # in case of linux
  [ -x /home/linuxbrew/.linuxbrew/bin/brew ] &&
    eval $(/home/linuxbrew/.linuxbrew/bin/brew shellenv)

  # in case of apple silicon
  [ -x /opt/homebrew/bin/brew ] &&
    eval $(/opt/homebrew/bin/brew shellenv)

  # in case of intel mac, it should already be loaded
}

_load_brew

function add-to-path {
  test -n "${1}" && path=("${1}" ${path})
}

# to mitigate the issue of /etc/profile not sourced for the time being - https://github.com/nix-community/home-manager/issues/3681
test -z "${path_helper_manually_called}" &&
  test -x /usr/libexec/path_helper &&
  eval $(/usr/libexec/path_helper -s) && export path_helper_manually_called=1

# assuming path for nix is already set from ../env
test -n "${float_nix_path}" && add-to-path $(printpath | grep 'nix-profile')

# this should be just after nix and before others
source "${my_dot_d}/zsh/path/tea"

# discover user specific binaries and scripts
add-to-path "${HOME}/.local/bin"

# actually this wouldn't work.
# so bootstrap script should be at its own repo
# and that should be sourced
# ---
# https://github.com/ryuheechul/bin will be clone to this
add-to-path "${HOME}/.local/my-bin"

# my custom scripts that should work on any platform
add-to-path "${my_dot_d}/bin/path/default"

# my custom scripts that should work on Linux
is-linux && add-to-path "${my_dot_d}/bin/path/linux"

# my custom scripts that should work on WSL
is-wsl && add-to-path "${my_dot_d}/bin/path/wsl"

# my custom scripts that should work on darwin
is-darwin && add-to-path "${my_dot_d}/bin/path/darwin"
# add path for ssh
is-ssh && add-to-path "${my_dot_d}/bin/path/ssh"

is-nixos && add-to-path "${my_dot_d}/bin/path/nixos"

test -n "${ADDITIONAL_PATH_FOR_EMACS}" &&
  add-to-path "$(echo ${ADDITIONAL_PATH_FOR_EMACS} | sed 's|^~|'"${HOME}"'|')"
